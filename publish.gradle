/**
 *  To publish on Maven local:
 *     ./gradlew -c [driverName]/publish-settings.gradle clean publishToMavenLocal
 *
 *  To publish on Bintray:
 *     - set environmental variables BINTRAY_USER and BINTRAY_API_KEY to proper values
 *     ./gradlew -c [driverName]/publish-settings.gradle clean bintrayUpload
 *
 */

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
    }
}

plugins {
    id 'com.jfrog.bintray' version '1.7.3'
    id 'com.github.ben-manes.versions' version '0.15.0'
}

allprojects {
    repositories {
        jcenter()
    }
}

apply from: 'build.gradle'
apply plugin: 'maven-publish'

task sourceJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

publishing {
    publications {
        driverPublish(MavenPublication) {
            groupId 'com.rosterloh.things.driver'
            artifactId "$project.name"
            version ARTIFACT_VERSION
            artifact sourceJar
            artifact("$buildDir/outputs/aar/$project.name-release.aar")
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.compile.allDependencies.each {
                    if(it.group != null && it.name != null &&
                    !"unspecified".equals(it.name) && it.version != null &&
                    !"unspecified".equals(it.version))
                    {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

publishToMavenLocal.dependsOn build
bintrayUpload.dependsOn build

bintray {
  user = System.getenv('BINTRAY_USER')
  key = System.getenv('BINTRAY_API_KEY')
  publications = ['driverPublish']

  publish = true

  pkg {
    repo = 'rosterloh'
    name = "androidthings-driver-$project.name"

    version {
      name = ARTIFACT_VERSION
      gpg {
        sign = true
      }
    }
  }
}